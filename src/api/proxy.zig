const std = @import("std");
const builtin = @import("builtin");
const std_compat = @import("compat");
const net_compat = @import("../net_compat.zig");

const Allocator = std.mem.Allocator;

pub const Response = struct {
    status: []const u8,
    content_type: []const u8,
    body: []const u8,
};

pub const ForwardOptions = struct {
    method: []const u8,
    base_url: []const u8,
    path: []const u8,
    body: []const u8,
    bearer_token: ?[]const u8 = null,
    content_type: []const u8 = "application/json",
    accept: ?[]const u8 = null,
    unreachable_body: []const u8 = "{\"error\":\"upstream unreachable\"}",
    max_response_bytes: ?usize = null,
    /// Per-read/write socket deadline for the upstream connection. A wedged
    /// upstream then yields a fast 504 instead of hanging the handler thread
    /// forever. 0 disables the deadline.
    timeout_ms: u32 = 15_000,
};

const timeout_response = Response{
    .status = "504 Gateway Timeout",
    .content_type = "application/json",
    .body = "{\"error\":\"upstream timed out\"}",
};

/// Watchdog bounding how long an upstream request may stay in flight, so a
/// wedged upstream cannot hold a connection thread forever.
///
/// SO_RCVTIMEO cannot be used here: std.Io's threaded backend treats EAGAIN
/// from a socket deadline as a bug and aborts. Instead a small timer thread
/// shuts down a dup'd handle of the upstream socket once the deadline
/// passes, which unblocks the stalled read/write with an ordinary error.
/// The dup keeps the kernel socket object alive for the timer, so a recycled
/// fd number can never be hit by mistake, and finish() never blocks.
const UpstreamDeadline = struct {
    const poll_slice_ms: u64 = 200;

    const Shared = struct {
        done: std.atomic.Value(bool) = std.atomic.Value(bool).init(false),
        timed_out: std.atomic.Value(bool) = std.atomic.Value(bool).init(false),
        refs: std.atomic.Value(u8) = std.atomic.Value(u8).init(2),
        dup_handle: std.c.fd_t,
        timeout_ms: u64,
    };

    shared: ?*Shared = null,

    fn start(connection: *std.http.Client.Connection, timeout_ms: u32) UpstreamDeadline {
        if (builtin.os.tag == .windows) return .{};
        const effective_ms = envTimeoutOverrideMs() orelse timeout_ms;
        if (effective_ms == 0) return .{};
        const handle = connection.stream_reader.stream.socket.handle;
        const dup_handle = std.c.dup(handle);
        if (dup_handle < 0) return .{};
        const shared = std.heap.smp_allocator.create(Shared) catch {
            _ = std.c.close(dup_handle);
            return .{};
        };
        shared.* = .{ .dup_handle = dup_handle, .timeout_ms = effective_ms };
        const thread = std.Thread.spawn(.{ .stack_size = 128 * 1024 }, timerMain, .{shared}) catch {
            _ = std.c.close(dup_handle);
            std.heap.smp_allocator.destroy(shared);
            return .{};
        };
        thread.detach();
        return .{ .shared = shared };
    }

    fn timerMain(shared: *Shared) void {
        var elapsed_ms: u64 = 0;
        while (elapsed_ms < shared.timeout_ms and !shared.done.load(.acquire)) {
            const slice: u64 = @min(poll_slice_ms, shared.timeout_ms - elapsed_ms);
            std_compat.thread.sleep(slice * std.time.ns_per_ms);
            elapsed_ms += slice;
        }
        if (!shared.done.load(.acquire)) {
            shared.timed_out.store(true, .release);
            _ = std.c.shutdown(shared.dup_handle, std.posix.SHUT.RDWR);
        }
        release(shared);
    }

    fn release(shared: *Shared) void {
        if (shared.refs.fetchSub(1, .acq_rel) == 1) {
            _ = std.c.close(shared.dup_handle);
            std.heap.smp_allocator.destroy(shared);
        }
    }

    fn timedOut(self: *const UpstreamDeadline) bool {
        const shared = self.shared orelse return false;
        return shared.timed_out.load(.acquire);
    }

    fn finish(self: *UpstreamDeadline) void {
        const shared = self.shared orelse return;
        shared.done.store(true, .release);
        release(shared);
        self.shared = null;
    }
};

/// Deadline override (NULLHUB_PROXY_TIMEOUT_MS) so regression tests can
/// exercise the watchdog without waiting out production deadlines.
fn envTimeoutOverrideMs() ?u32 {
    if (builtin.os.tag == .windows) return null;
    const value = std.c.getenv("NULLHUB_PROXY_TIMEOUT_MS") orelse return null;
    return std.fmt.parseInt(u32, std.mem.span(value), 10) catch null;
}

const LimitedResponseBody = struct {
    body: std.Io.Writer.Allocating,
    writer: std.Io.Writer,
    buffer_storage: [16 * 1024]u8 = undefined,
    limit: usize,
    written: usize = 0,
    too_large: bool = false,

    fn init(allocator: Allocator, max_response_bytes: ?usize) LimitedResponseBody {
        return .{
            .body = .init(allocator),
            .writer = .{
                .vtable = &vtable,
                .buffer = &.{},
            },
            .limit = max_response_bytes orelse std.math.maxInt(usize),
        };
    }

    fn prepare(self: *LimitedResponseBody) void {
        self.writer = .{
            .vtable = &vtable,
            .buffer = &self.buffer_storage,
        };
    }

    fn deinit(self: *LimitedResponseBody) void {
        self.body.deinit();
    }

    fn toOwnedSlice(self: *LimitedResponseBody) ![]u8 {
        self.writer.flush() catch return error.WriteFailed;
        return try self.body.toOwnedSlice();
    }

    const vtable: std.Io.Writer.VTable = .{
        .drain = drain,
        .flush = flush,
    };

    fn drain(writer: *std.Io.Writer, data: []const []const u8, splat: usize) std.Io.Writer.Error!usize {
        const self: *LimitedResponseBody = @fieldParentPtr("writer", writer);
        if (data.len == 0) return 0;
        const buffered = writer.buffer[0..writer.end];
        const data_total = std.Io.Writer.countSplat(data, splat);
        const total = std.math.add(usize, buffered.len, data_total) catch {
            self.too_large = true;
            return error.WriteFailed;
        };
        const next_written = std.math.add(usize, self.written, total) catch {
            self.too_large = true;
            return error.WriteFailed;
        };
        if (next_written > self.limit) {
            self.too_large = true;
            return error.WriteFailed;
        }

        if (buffered.len > 0) {
            self.body.writer.writeAll(buffered) catch return error.WriteFailed;
            writer.end = 0;
        }
        for (data[0 .. data.len - 1]) |bytes| {
            self.body.writer.writeAll(bytes) catch return error.WriteFailed;
        }
        const pattern = data[data.len - 1];
        for (0..splat) |_| {
            self.body.writer.writeAll(pattern) catch return error.WriteFailed;
        }
        self.written = next_written;
        return data_total;
    }

    fn flush(writer: *std.Io.Writer) std.Io.Writer.Error!void {
        const self: *LimitedResponseBody = @fieldParentPtr("writer", writer);
        if (writer.end > 0) {
            _ = try drain(writer, &.{""}, 1);
        }
        try self.body.writer.flush();
    }
};

pub fn isPathInNamespace(target: []const u8, prefix: []const u8) bool {
    return std.mem.eql(u8, target, prefix) or
        (target.len > prefix.len and
            std.mem.startsWith(u8, target, prefix) and
            target[prefix.len] == '/');
}

pub fn isTargetInNamespace(target: []const u8, prefix: []const u8) bool {
    const path = if (std.mem.indexOfScalar(u8, target, '?')) |idx| target[0..idx] else target;
    return isPathInNamespace(path, prefix);
}

pub const ProductProxyRewriteOptions = struct {
    prefix: []const u8,
    selector_params: []const []const u8 = &.{},
    default_path: []const u8 = "/",
};

pub const ProductProxyTarget = struct {
    target: []const u8,
    path: []const u8,
    target_owned: bool = false,
    path_owned: bool = false,

    pub fn deinit(self: *ProductProxyTarget, allocator: Allocator) void {
        if (self.path_owned) allocator.free(self.path);
        if (self.target_owned) allocator.free(self.target);
        self.* = .{ .target = "", .path = "" };
    }
};

pub fn rewriteProductProxyTarget(allocator: Allocator, target: []const u8, opts: ProductProxyRewriteOptions) !ProductProxyTarget {
    var stripped = try stripSelectorParams(allocator, target, opts.selector_params);
    errdefer stripped.deinit(allocator);

    const suffix = stripped.value[opts.prefix.len..];
    if (suffix.len == 0) {
        return .{
            .target = stripped.value,
            .path = opts.default_path,
            .target_owned = stripped.owned,
        };
    }

    if (suffix[0] == '?') {
        return .{
            .target = stripped.value,
            .path = try std.fmt.allocPrint(allocator, "{s}{s}", .{ opts.default_path, suffix }),
            .target_owned = stripped.owned,
            .path_owned = true,
        };
    }

    return .{
        .target = stripped.value,
        .path = suffix,
        .target_owned = stripped.owned,
    };
}

const StrippedTarget = struct {
    value: []const u8,
    owned: bool = false,

    fn deinit(self: *StrippedTarget, allocator: Allocator) void {
        if (self.owned) allocator.free(self.value);
        self.* = .{ .value = "" };
    }
};

fn stripSelectorParams(allocator: Allocator, target: []const u8, selector_params: []const []const u8) !StrippedTarget {
    const qmark = std.mem.indexOfScalar(u8, target, '?') orelse return .{ .value = target };
    if (selector_params.len == 0) return .{ .value = target };

    var stripped_any = false;
    var buf = std.array_list.Managed(u8).init(allocator);
    errdefer buf.deinit();

    try buf.appendSlice(target[0..qmark]);
    var wrote_query = false;
    var params = std.mem.splitScalar(u8, target[qmark + 1 ..], '&');
    while (params.next()) |param| {
        if (param.len == 0) continue;
        if (isSelectorParam(param, selector_params)) {
            stripped_any = true;
            continue;
        }
        try buf.append(if (wrote_query) '&' else '?');
        wrote_query = true;
        try buf.appendSlice(param);
    }

    if (!stripped_any) {
        buf.deinit();
        return .{ .value = target };
    }
    return .{ .value = try buf.toOwnedSlice(), .owned = true };
}

fn isSelectorParam(param: []const u8, selector_params: []const []const u8) bool {
    const key = if (std.mem.indexOfScalar(u8, param, '=')) |eq| param[0..eq] else param;
    for (selector_params) |selector| {
        if (std.mem.eql(u8, key, selector)) return true;
    }
    return false;
}

pub fn forward(allocator: Allocator, opts: ForwardOptions) Response {
    const http_method = parseMethod(opts.method) orelse
        return .{ .status = "405 Method Not Allowed", .content_type = "application/json", .body = "{\"error\":\"method not allowed\"}" };

    const url = std.fmt.allocPrint(allocator, "{s}{s}", .{ opts.base_url, opts.path }) catch
        return .{ .status = "500 Internal Server Error", .content_type = "application/json", .body = "{\"error\":\"internal error\"}" };
    defer allocator.free(url);

    const uri = std.Uri.parse(url) catch
        return .{ .status = "502 Bad Gateway", .content_type = "application/json", .body = opts.unreachable_body };

    var auth_header: ?[]const u8 = null;
    defer if (auth_header) |value| allocator.free(value);
    var header_buf: [3]std.http.Header = undefined;
    var header_count: usize = 0;
    if (opts.body.len > 0 and opts.content_type.len > 0) {
        header_buf[header_count] = .{ .name = "Content-Type", .value = opts.content_type };
        header_count += 1;
    }
    if (opts.accept) |accept| {
        header_buf[header_count] = .{ .name = "Accept", .value = accept };
        header_count += 1;
    }
    const extra_headers: []const std.http.Header = if (opts.bearer_token) |token| blk: {
        auth_header = std.fmt.allocPrint(allocator, "Bearer {s}", .{token}) catch
            return .{ .status = "500 Internal Server Error", .content_type = "application/json", .body = "{\"error\":\"internal error\"}" };
        header_buf[header_count] = .{ .name = "Authorization", .value = auth_header.? };
        header_count += 1;
        break :blk header_buf[0..header_count];
    } else header_buf[0..header_count];

    var client: std.http.Client = .{ .allocator = allocator, .io = std_compat.io() };
    defer client.deinit();

    var response_body = LimitedResponseBody.init(allocator, opts.max_response_bytes);
    response_body.prepare();
    defer response_body.deinit();

    var request = client.request(http_method, uri, .{
        .redirect_behavior = .unhandled,
        .keep_alive = false,
        .headers = .{
            .accept_encoding = .omit,
            .connection = .{ .override = "close" },
        },
        .extra_headers = extra_headers,
    }) catch
        return .{ .status = "502 Bad Gateway", .content_type = "application/json", .body = opts.unreachable_body };
    defer request.deinit();

    var deadline: UpstreamDeadline = .{};
    if (request.connection) |connection| deadline = UpstreamDeadline.start(connection, opts.timeout_ms);
    defer deadline.finish();

    const unreachable_response = Response{ .status = "502 Bad Gateway", .content_type = "application/json", .body = opts.unreachable_body };

    if (opts.body.len > 0) {
        request.transfer_encoding = .{ .content_length = opts.body.len };
        var body_writer = request.sendBodyUnflushed(&.{}) catch
            return if (deadline.timedOut()) timeout_response else unreachable_response;
        body_writer.writer.writeAll(opts.body) catch
            return if (deadline.timedOut()) timeout_response else unreachable_response;
        body_writer.end() catch
            return if (deadline.timedOut()) timeout_response else unreachable_response;
        request.connection.?.flush() catch
            return if (deadline.timedOut()) timeout_response else unreachable_response;
    } else {
        request.sendBodiless() catch
            return if (deadline.timedOut()) timeout_response else unreachable_response;
    }

    var response = request.receiveHead(&.{}) catch
        return if (deadline.timedOut()) timeout_response else unreachable_response;

    var transfer_buffer: [64]u8 = undefined;
    const reader = response.reader(&transfer_buffer);
    _ = reader.streamRemaining(&response_body.writer) catch |err| switch (err) {
        error.WriteFailed => if (response_body.too_large)
            return .{ .status = "502 Bad Gateway", .content_type = "application/json", .body = "{\"error\":\"upstream response too large\"}" }
        else
            return .{ .status = "500 Internal Server Error", .content_type = "application/json", .body = "{\"error\":\"internal error\"}" },
        error.ReadFailed => return if (deadline.timedOut()) timeout_response else unreachable_response,
    };

    const resp_body = response_body.toOwnedSlice() catch
        return .{ .status = "500 Internal Server Error", .content_type = "application/json", .body = "{\"error\":\"internal error\"}" };

    const status_code = @intFromEnum(response.head.status);
    return .{
        .status = mapStatus(status_code),
        .content_type = if (status_code >= 200 and status_code < 300) (opts.accept orelse "application/json") else "application/json",
        .body = resp_body,
    };
}

pub const TestUpstream = struct {
    allocator: Allocator,
    ctx: *Context,
    thread: std.Thread,

    const Context = struct {
        server: std_compat.net.Server,
        stop_flag: std.atomic.Value(bool),
        response: []u8,
        request_buf: [4096]u8 = undefined,
        request_len: usize = 0,

        fn run(ctx: *Context) void {
            while (!ctx.stop_flag.load(.acquire)) {
                var conn = ctx.server.accept() catch |err| switch (err) {
                    error.WouldBlock => {
                        std_compat.thread.sleep(10 * std.time.ns_per_ms);
                        continue;
                    },
                    else => return,
                };
                defer conn.stream.close();

                ctx.request_len = conn.stream.read(&ctx.request_buf) catch return;
                _ = conn.stream.write(ctx.response) catch return;
                return;
            }
        }
    };

    pub fn start(allocator: Allocator, response: []const u8) !TestUpstream {
        const response_owned = try allocator.dupe(u8, response);
        errdefer allocator.free(response_owned);

        const ctx = try allocator.create(Context);
        errdefer allocator.destroy(ctx);
        ctx.* = .{
            .server = undefined,
            .stop_flag = std.atomic.Value(bool).init(false),
            .response = response_owned,
        };

        const addr = try std_compat.net.Address.resolveIp("127.0.0.1", 0);
        ctx.server = try addr.listen(.{});
        errdefer ctx.server.deinit();

        const thread = try std.Thread.spawn(.{}, Context.run, .{ctx});

        return .{
            .allocator = allocator,
            .ctx = ctx,
            .thread = thread,
        };
    }

    pub fn deinit(self: *TestUpstream) void {
        self.ctx.stop_flag.store(true, .release);
        self.thread.join();
        self.ctx.server.deinit();
        self.allocator.free(self.ctx.response);
        self.allocator.destroy(self.ctx);
    }

    pub fn baseUrl(self: *const TestUpstream, allocator: Allocator) ![]const u8 {
        return std.fmt.allocPrint(allocator, "http://127.0.0.1:{d}", .{self.ctx.server.listen_address.in.getPort()});
    }

    pub fn request(self: *const TestUpstream) []const u8 {
        return self.ctx.request_buf[0..self.ctx.request_len];
    }
};

pub fn forwardStream(allocator: Allocator, opts: ForwardOptions, downstream: std_compat.net.Stream, cors_headers: []const u8) !void {
    const http_method = parseMethod(opts.method) orelse {
        try writeDirectResponse(downstream, "405 Method Not Allowed", "application/json", "{\"error\":\"method not allowed\"}", cors_headers);
        return;
    };
    const url = try std.fmt.allocPrint(allocator, "{s}{s}", .{ opts.base_url, opts.path });
    defer allocator.free(url);
    const uri = std.Uri.parse(url) catch {
        try writeDirectResponse(downstream, "502 Bad Gateway", "application/json", opts.unreachable_body, cors_headers);
        return;
    };

    var auth_header: ?[]const u8 = null;
    defer if (auth_header) |value| allocator.free(value);
    var header_buf: [3]std.http.Header = undefined;
    var header_count: usize = 0;
    if (opts.body.len > 0 and opts.content_type.len > 0) {
        header_buf[header_count] = .{ .name = "Content-Type", .value = opts.content_type };
        header_count += 1;
    }
    if (opts.accept) |accept| {
        header_buf[header_count] = .{ .name = "Accept", .value = accept };
        header_count += 1;
    }
    const extra_headers: []const std.http.Header = if (opts.bearer_token) |token| blk: {
        auth_header = try std.fmt.allocPrint(allocator, "Bearer {s}", .{token});
        header_buf[header_count] = .{ .name = "Authorization", .value = auth_header.? };
        header_count += 1;
        break :blk header_buf[0..header_count];
    } else header_buf[0..header_count];

    var client: std.http.Client = .{ .allocator = allocator, .io = std_compat.io() };
    defer client.deinit();

    var request = client.request(http_method, uri, .{
        .redirect_behavior = .unhandled,
        .keep_alive = false,
        .headers = .{
            .accept_encoding = .omit,
            .connection = .{ .override = "close" },
        },
        .extra_headers = extra_headers,
    }) catch {
        try writeDirectResponse(downstream, "502 Bad Gateway", "application/json", opts.unreachable_body, cors_headers);
        return;
    };
    defer request.deinit();

    var deadline: UpstreamDeadline = .{};
    if (request.connection) |connection| deadline = UpstreamDeadline.start(connection, opts.timeout_ms);
    defer deadline.finish();

    if (http_method.requestHasBody()) {
        request.transfer_encoding = .{ .content_length = opts.body.len };
        var body_buffer: [8192]u8 = undefined;
        var body_writer = request.sendBodyUnflushed(&body_buffer) catch {
            try writeDirectResponse(downstream, "502 Bad Gateway", "application/json", opts.unreachable_body, cors_headers);
            return;
        };
        body_writer.writer.writeAll(opts.body) catch {
            try writeDirectResponse(downstream, "502 Bad Gateway", "application/json", opts.unreachable_body, cors_headers);
            return;
        };
        body_writer.end() catch {
            try writeDirectResponse(downstream, "502 Bad Gateway", "application/json", opts.unreachable_body, cors_headers);
            return;
        };
        request.connection.?.flush() catch {
            try writeDirectResponse(downstream, "502 Bad Gateway", "application/json", opts.unreachable_body, cors_headers);
            return;
        };
    } else {
        request.sendBodiless() catch {
            try writeDirectResponse(downstream, "502 Bad Gateway", "application/json", opts.unreachable_body, cors_headers);
            return;
        };
    }

    var response = request.receiveHead(&.{}) catch {
        if (deadline.timedOut()) {
            try writeDirectResponse(downstream, "504 Gateway Timeout", "application/json", "{\"error\":\"upstream timed out\"}", cors_headers);
        } else {
            try writeDirectResponse(downstream, "502 Bad Gateway", "application/json", opts.unreachable_body, cors_headers);
        }
        return;
    };
    const status_code = @intFromEnum(response.head.status);
    const content_type = response.head.content_type orelse
        if (status_code >= 200 and status_code < 300) (opts.accept orelse "application/octet-stream") else "application/json";

    try writeStreamingResponseHeaders(downstream, mapStatus(status_code), content_type, cors_headers);

    var transfer_buffer: [64]u8 = undefined;
    const reader = response.reader(&transfer_buffer);
    var read_buf: [16 * 1024]u8 = undefined;
    while (true) {
        const n = reader.readSliceShort(&read_buf) catch |err| {
            std.log.warn("upstream stream read failed: {s}", .{@errorName(err)});
            return;
        };
        if (n == 0) return;
        try net_compat.streamWriteAll(downstream, read_buf[0..n]);
    }
}

fn writeDirectResponse(stream: std_compat.net.Stream, status: []const u8, content_type: []const u8, body: []const u8, cors_headers: []const u8) !void {
    var buf: [4096]u8 = undefined;
    var writer: std.Io.Writer = .fixed(&buf);
    try writer.print("HTTP/1.1 {s}\r\n", .{status});
    try writer.print("Content-Type: {s}\r\n", .{content_type});
    try writer.print("Content-Length: {d}\r\n", .{body.len});
    try writer.writeAll(cors_headers);
    try writer.writeAll("Connection: close\r\n\r\n");
    if (body.len <= buf.len - writer.buffered().len) {
        try writer.writeAll(body);
        try net_compat.streamWriteAll(stream, writer.buffered());
        return;
    }
    try net_compat.streamWriteAll(stream, writer.buffered());
    if (body.len > 0) try net_compat.streamWriteAll(stream, body);
}

fn writeStreamingResponseHeaders(stream: std_compat.net.Stream, status: []const u8, content_type: []const u8, cors_headers: []const u8) !void {
    var buf: [4096]u8 = undefined;
    var writer: std.Io.Writer = .fixed(&buf);
    try writer.print("HTTP/1.1 {s}\r\n", .{status});
    try writer.print("Content-Type: {s}\r\n", .{content_type});
    try writer.writeAll("Cache-Control: no-cache\r\n");
    try writer.writeAll("X-Accel-Buffering: no\r\n");
    try writer.writeAll(cors_headers);
    try writer.writeAll("Connection: close\r\n\r\n");
    try net_compat.streamWriteAll(stream, writer.buffered());
}

fn parseMethod(method: []const u8) ?std.http.Method {
    if (std.mem.eql(u8, method, "GET")) return .GET;
    if (std.mem.eql(u8, method, "POST")) return .POST;
    if (std.mem.eql(u8, method, "PUT")) return .PUT;
    if (std.mem.eql(u8, method, "DELETE")) return .DELETE;
    if (std.mem.eql(u8, method, "PATCH")) return .PATCH;
    return null;
}

fn mapStatus(code: u10) []const u8 {
    return switch (code) {
        100 => "100 Continue",
        101 => "101 Switching Protocols",
        102 => "102 Processing",
        103 => "103 Early Hints",
        200 => "200 OK",
        201 => "201 Created",
        202 => "202 Accepted",
        203 => "203 Non-Authoritative Information",
        204 => "204 No Content",
        205 => "205 Reset Content",
        206 => "206 Partial Content",
        207 => "207 Multi-Status",
        208 => "208 Already Reported",
        226 => "226 IM Used",
        300 => "300 Multiple Choices",
        301 => "301 Moved Permanently",
        302 => "302 Found",
        303 => "303 See Other",
        304 => "304 Not Modified",
        305 => "305 Use Proxy",
        307 => "307 Temporary Redirect",
        308 => "308 Permanent Redirect",
        400 => "400 Bad Request",
        401 => "401 Unauthorized",
        402 => "402 Payment Required",
        403 => "403 Forbidden",
        404 => "404 Not Found",
        405 => "405 Method Not Allowed",
        406 => "406 Not Acceptable",
        407 => "407 Proxy Authentication Required",
        408 => "408 Request Timeout",
        409 => "409 Conflict",
        410 => "410 Gone",
        411 => "411 Length Required",
        412 => "412 Precondition Failed",
        413 => "413 Payload Too Large",
        414 => "414 URI Too Long",
        415 => "415 Unsupported Media Type",
        416 => "416 Range Not Satisfiable",
        417 => "417 Expectation Failed",
        418 => "418 I'm a Teapot",
        421 => "421 Misdirected Request",
        422 => "422 Unprocessable Entity",
        423 => "423 Locked",
        424 => "424 Failed Dependency",
        425 => "425 Too Early",
        426 => "426 Upgrade Required",
        428 => "428 Precondition Required",
        429 => "429 Too Many Requests",
        431 => "431 Request Header Fields Too Large",
        451 => "451 Unavailable For Legal Reasons",
        500 => "500 Internal Server Error",
        501 => "501 Not Implemented",
        502 => "502 Bad Gateway",
        503 => "503 Service Unavailable",
        504 => "504 Gateway Timeout",
        505 => "505 HTTP Version Not Supported",
        506 => "506 Variant Also Negotiates",
        507 => "507 Insufficient Storage",
        508 => "508 Loop Detected",
        510 => "510 Not Extended",
        511 => "511 Network Authentication Required",
        else => if (code >= 200 and code < 300) "200 OK" else if (code >= 400 and code < 500) "400 Bad Request" else "500 Internal Server Error",
    };
}

test "isPathInNamespace matches exact and slash-delimited paths" {
    try std.testing.expect(isPathInNamespace("/api/nullwatch", "/api/nullwatch"));
    try std.testing.expect(isPathInNamespace("/api/nullwatch/v1/runs", "/api/nullwatch"));
    try std.testing.expect(isPathInNamespace("/api/nullwatch/v1/runs?limit=1", "/api/nullwatch"));
    try std.testing.expect(!isPathInNamespace("/api/nullwatch?limit=1", "/api/nullwatch"));
    try std.testing.expect(!isPathInNamespace("/api/nullwatch-extra", "/api/nullwatch"));
    try std.testing.expect(!isPathInNamespace("/api/nullboiler", "/api/nullwatch"));
}

test "isTargetInNamespace matches query targets by path only" {
    try std.testing.expect(isTargetInNamespace("/api/nullwatch?limit=1", "/api/nullwatch"));
    try std.testing.expect(isTargetInNamespace("/api/nullwatch/v1/runs?limit=1", "/api/nullwatch"));
    try std.testing.expect(!isTargetInNamespace("/api/nullwatch-extra?limit=1", "/api/nullwatch"));
}

test "rewriteProductProxyTarget strips selector params and public prefix" {
    const allocator = std.testing.allocator;
    const selector_params = [_][]const u8{"nullhub_watch"};

    var rewritten = try rewriteProductProxyTarget(
        allocator,
        "/api/nullwatch/v1/runs?limit=50&nullhub_watch=alpha&status=ok",
        .{
            .prefix = "/api/nullwatch",
            .selector_params = selector_params[0..],
            .default_path = "/v1/summary",
        },
    );
    defer rewritten.deinit(allocator);

    try std.testing.expectEqualStrings("/api/nullwatch/v1/runs?limit=50&status=ok", rewritten.target);
    try std.testing.expectEqualStrings("/v1/runs?limit=50&status=ok", rewritten.path);
}

test "rewriteProductProxyTarget keeps similarly named upstream params" {
    const allocator = std.testing.allocator;
    const selector_params = [_][]const u8{"boiler_instance"};

    var rewritten = try rewriteProductProxyTarget(
        allocator,
        "/api/nullboiler/runs?boiler_instance_id=upstream&boiler_instance=local&status=running",
        .{
            .prefix = "/api/nullboiler",
            .selector_params = selector_params[0..],
        },
    );
    defer rewritten.deinit(allocator);

    try std.testing.expectEqualStrings("/api/nullboiler/runs?boiler_instance_id=upstream&status=running", rewritten.target);
    try std.testing.expectEqualStrings("/runs?boiler_instance_id=upstream&status=running", rewritten.path);
}

test "rewriteProductProxyTarget keeps root upstream filters on default path" {
    const allocator = std.testing.allocator;
    const selector_params = [_][]const u8{"nullhub_watch"};

    var rewritten = try rewriteProductProxyTarget(
        allocator,
        "/api/nullwatch?nullhub_watch=alpha&watch=upstream",
        .{
            .prefix = "/api/nullwatch",
            .selector_params = selector_params[0..],
            .default_path = "/v1/summary",
        },
    );
    defer rewritten.deinit(allocator);

    try std.testing.expectEqualStrings("/api/nullwatch?watch=upstream", rewritten.target);
    try std.testing.expectEqualStrings("/v1/summary?watch=upstream", rewritten.path);
}

test "limited response writer drains buffered bytes during rebase" {
    const allocator = std.testing.allocator;
    var limited = LimitedResponseBody.init(allocator, null);
    limited.prepare();
    defer limited.deinit();

    const payload = try allocator.alloc(u8, 20 * 1024);
    defer allocator.free(payload);
    @memset(payload, 'x');

    try limited.writer.writeAll(payload);
    const body = try limited.toOwnedSlice();
    defer allocator.free(body);

    try std.testing.expectEqual(payload.len, body.len);
    try std.testing.expect(std.mem.eql(u8, payload, body));
}

test "limited response writer enforces limit for buffered bytes on flush" {
    const allocator = std.testing.allocator;
    var limited = LimitedResponseBody.init(allocator, 8);
    limited.prepare();
    defer limited.deinit();

    try limited.writer.writeAll("123456789");

    try std.testing.expectError(error.WriteFailed, limited.toOwnedSlice());
    try std.testing.expect(limited.too_large);
}

test "mapStatus preserves common upstream status codes" {
    try std.testing.expectEqualStrings("202 Accepted", mapStatus(202));
    try std.testing.expectEqualStrings("206 Partial Content", mapStatus(206));
    try std.testing.expectEqualStrings("429 Too Many Requests", mapStatus(429));
    try std.testing.expectEqualStrings("504 Gateway Timeout", mapStatus(504));
}
